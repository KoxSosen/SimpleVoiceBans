plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    version = '1.4-SNAPSHOT'
    group = 'com.github.koxsosen'

    dependencies {
        compileOnly 'de.maxhenkel.voicechat:voicechat-api:2.5.0'
        compileOnly 'space.arim.libertybans:bans-api:1.1.0-SNAPSHOT'
    }

    repositories {
        maven { url = 'https://maven.maxhenkel.de/repository/public' }
        maven {
            name= 'arim-mvn-lgpl3'
            url = 'https://mvn-repo.arim.space/lesser-gpl3/'
        }
        maven {
            name= 'arim-mvn-gpl3'
            url = 'https://mvn-repo.arim.space/gpl3/'
        }
        maven {
            name= 'arim-mvn-agpl3'
            url = 'https://mvn-repo.arim.space/affero-gpl3/'
        }
    }

    tasks.register('uberJar', Jar) {
        archiveClassifier = 'uber'
        archiveBaseName = 'SimpleVoiceBans'
        archiveAppendix = project.name

        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        from sourceSets.main.output

        dependsOn configurations.runtimeClasspath
        from {
            configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
        }
    }

    shadowJar {
        archiveClassifier = 'uber'
        archiveBaseName = 'SimpleVoiceBans'
        archiveAppendix = project.name

        relocate 'com.github.benmanes.caffeine.cache', 'com.github.simplevbans.benmanes.caffeine.cache'
        relocate 'space.arim.morepaperlib', 'space.arim.simplevbans.morepaperlib'
    }



    compileJava   {
        sourceCompatibility = '17'
        targetCompatibility = '17'
    }

}